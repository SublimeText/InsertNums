# insert_spec   ::=  insertnum | insertalpha

insertnum     ::=  [signednum][:signednum][~format][::expr][@stopexpr] ["!"]

signednum     ::=  ["+" | "-"] (integer | float)
numeric       ::=  integer | float
expr          ::=  <a character other than '!' or @>+
stop_expr     ::=  <a character other than '!'>+

# essentially Python's "str.format" syntax
format        ::=  [[fill]align][sign][#][0][width][.precision][type]
fill          ::=  <a character other than '}'>
align         ::=  "<" | ">" | "=" | "^"
sign          ::=  "+" | "-" | " "
width         ::=  integer
precision     ::=  integer
type          ::=  "b" | "c" | "d" | "e" | "E" | "f" | "F" | "g" | "G" | "n" | "o" | "x" | "X" | "%"

insertalpha   ::=  alphastart[:signedint][~[alphaformat] ["w"]] ["!"]

alphastart    ::=  alphalower+ | alphaupper+
alphalower    ::=  "a"..."z"
alphaupper    ::=  "A"..."Z"
alphaformat   ::=  [[fill]align][width]

####################

signedint     ::=  ["+" | "-"] {integer}
integer       ::=  nonzerodigit digit* | "0"
nonzerodigit  ::=  "1"..."9"
digit         ::=  "0"..."9"

float         ::=  pointfloat | exponentfloat
pointfloat    ::=  [intpart] fraction | intpart "."
exponentfloat ::=  (intpart | pointfloat) exponent
intpart       ::=  digit+
fraction      ::=  "." digit+
exponent      ::=  ("e" | "E") ["+" | "-"] digit+
